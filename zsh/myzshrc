# Beep off on error in ZLE
setopt no_beep

# Editor
if type "vim" > /dev/null 2>&1; then
  export EDITOR="vim"
fi

# Inform gpg-agent of the current TTY for user prompts.
# If you want to check gpg, type:
#   echo 'test' | gpg --clearsign
#
# BUT, Unable to List GPG Keys in Dev Container or Sign Git Commits
# https://github.com/microsoft/vscode-remote-release/issues/8549
export GPG_TTY=$TTY

# reverse-i-search with fzf
function select-history() {
  BUFFER=$(history -n -r 1 | fzf --no-sort +m --query "$LBUFFER" --prompt="History > ")
  CURSOR=$#BUFFER
}
zle -N select-history
bindkey '^r' select-history

# fgitshow - git commit browser
fgitshow() {
  git log --graph --color=always \
      --format="%C(auto)%h%d %s %C(black)%C(bold)%cr" "$@" |
  fzf --ansi --no-sort --reverse --tiebreak=index --bind=ctrl-s:toggle-sort \
      --bind "ctrl-m:execute:
                (grep -o '[a-f0-9]\{7\}' | head -1 |
                xargs -I % sh -c 'git show --color=always % | less -R') << 'FZF-EOF'
                {}
FZF-EOF"
}

# fcd - cd to selected directory
fcd() {
  local dir
  dir=$(find ${1:-.} -path '*/\.*' -prune \
                  -o -type d -print 2> /dev/null | fzf +m) &&
  cd "$dir"
}

#
# rbenv
#
[[ -d $HOME/.rbenv ]] && \
  export PATH=${HOME}/.rbenv/bin:${PATH} && \
  eval "$(rbenv init - zsh)"

#
# Alias
#
alias a='exa'

# trash-cli
# https://github.com/andreafrancia/trash-cli
if type trash-put > /dev/null 2>&1; then
  alias rm='trash-put'
fi
